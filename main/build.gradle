// A minimal example Java project that uses the protobuf plugin.
// To build it:
// $ ../gradlew clean build

apply plugin: 'java'
apply plugin: 'com.google.protobuf'
apply plugin: 'idea'
apply plugin: 'eclipse'


repositories {
    maven { url "https://plugins.gradle.org/m2/" }
}

buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.5'
    }
}

dependencies {
    compile 'com.google.protobuf:protobuf-java:3.19.4'
    compile 'io.grpc:grpc-stub:1.0.0-pre2'
    compile 'io.grpc:grpc-protobuf:1.0.0-pre2'
    if (JavaVersion.current().isJava8Compatible()) {
        // Workaround for @javax.annotation.Generated
        // see: https://github.com/grpc/grpc-java/issues/3633
        compile 'javax.annotation:javax.annotation-api:1.3.1'
    }
    // Extra proto source files besides the ones residing under
    // "src/main".

//    protobuf files("lib/protos.tar.gz")
    protobuf files("lib/protos.tar.gz")
    protobuf fileTree("ext/")

    testCompile 'junit:junit:4.12'
    // Extra proto source files for test besides the ones residing under
    // "src/test".
    testProtobuf files("lib/protos-test.tar.gz")
}

protobuf {
    protoc {
        // The artifact spec for the Protobuf Compiler
        artifact = 'com.google.protobuf:protoc:3.19.4'
    }
    plugins {
        // Optional: an artifact spec for a protoc plugin, with "grpc" as
        // the identifier, which can be referred to in the "plugins"
        // container of the "generateProtoTasks" closure.
        grpc {
            artifact = 'custom-protoc-plugin:0.2.1-alpha'
        }
    }
    generateProtoTasks {
        ofSourceSet('main')*.plugins {
            // Apply the "grpc" plugin whose spec is defined above, without
            // options.  Note the braces cannot be omitted, otherwise the
            // plugin will not be added. This is because of the implicit way
            // NamedDomainObjectContainer binds the methods.
            grpc { }
        }
    }
}
